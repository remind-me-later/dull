1	"BOMBARDEMENTS"
2	"E.BEAUREPAIRE 1983"
10	"A" CLEAR : PAUSE " **** BOMBARDEMENTS ****"
20	INPUT "Explanations (Y/N) ? "; A$ : IF A$ = "Y" THEN GOSUB 340
30	WAIT 0: CLS : RANDOM
40	FOR J = 0 TO 12 : GPRINT 128; : NEXT J
50	LET A$ = "7870604080"
60	FOR J = 0 TO 99 : GPRINT MID$(A$, RND(5)*2-1, 2); : NEXT J
70	GPRINT 128
80	LET A = 1, E = 1
90	FOR P = 1 TO 113: GCURSOR P-1
100	LET R = I, I = POINT(P)
110	GPRINT R; I OR A
120	IF A AND I THEN GOTO 290
130	IF B > 0 THEN GOTO 160
140	IF INKEY$ <> " " THEN GOTO 190
150	BEEP 1,20,4 : LET C = A, D = P, S = POINT D-A
160	LET B = B+.5:IF B=INT(B) THEN LET C=C*2
170	GCURSOR D: GPRINT S OR C
180	IF S AND C THEN GCURSOR D: GOSUB 220
190	NEXT P
200	LET A=A*2, E=E+1 : IF E > 4 THEN GOSUB 270
210	GCURSOR 113:GPRINT 128:GOTO 90
215 REM SUBROUTINE
220	BEEP 1,8,6:IF S=120 THEN GPRINT 112:W=W+4:GOTO 260
230	IF S=112 THEN GPRINT 96:W=W+3:GOTO 260
240	IF S=96 THEN GPRINT 64:W=W+2:GOTO 260
250	IF S=64 THEN GPRINT 128:W=W+1
260	LET B=0:CURSOR 22:PRINT USING "####";W:RETURN
265 REM SUBROUTINE
270	LET F=(E-4)*50:CURSOR 22:PRINT USING "*+###";F:LET W=W+F
280	BEEP 3,100,100:CURSOR 22:PRINT USING "####";W:RETURN
285 REM SUBROUTINE
290	GCURSOR P-2:PRINT "*"
300	FOR I=0 TO 20: POKE# 64000, RND 256-1: NEXT I
310	WAIT :USING :PRINT "*** SCORE *** :";W
330	END
340	PAUSE "Destroying large blocs..."
350	PAUSE "gives more points."
360	PAUSE "You fly closer to the..."
370	PAUSE "ground at each passage."
380	PAUSE "SPACE : drop bomb (1/time).": PAUSE
390	RETURN